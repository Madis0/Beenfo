buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.2'
    }
    repositories {
        maven {
            name = 'CrowdinTranslate source'
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'beenfo'

repositories {
	maven {
		url = "https://maven.fabricmc.net/"
	}
    maven {
        url = "https://minecraft.guntram.de/maven/"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.16.4.properties").newReader())

archivesBaseName = "beenfo"
ext.projectVersion = "1.3"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

minecraft {
    refmapName = "Beenfo-refmap.json";
}

processResources {
	inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

dependencies {
    
    // This sucks, but the spigot server part needs Craft... classes which
    // aren't in spigots maven, so if you want to compile the mod, you need
    // to get a spigot.jar first. Sorry.

    compileOnly files('/home/gbl/SoftwareProjects/MyProjects/MinecraftPlugins/libraries/spigot-1.16.4.jar')

    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "io.github.prospector:modmenu:${Versions['modmenu_version']}"
    modImplementation "de.guntram.mcmod:crowdin-translate:1.2"
    include "de.guntram.mcmod:crowdin-translate:1.2"
    modImplementation "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
    include    "de.guntram.mcmod:GBfabrictools:${Versions['gbfabrictools_version']}"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
    dependsOn downloadTranslations
	from "LICENSE"
}
